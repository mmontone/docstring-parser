(in-package #:docstring-parser)

(defstruct (list-element
             (:print-function print-list-element))
  items)

(defun print-list-element (elem stream depth)
  (format stream "(:list 窿扉篝屐屙孱舡轸屙屐屙┅ㄤ彐篝蝓泗扉篝轸屙屐屙孱ê痱轭舡骢钽糸镱痱轭舡扉篝轸屙屐屙孱舂翦舂ㄤ彐躅痱轭舡扉篝轸屙屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê扉萤扉篝轸屙屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄩ翎扉悱屐屙孱ê痱轭舡骢钽糸镱痱轭舡轸犰殂屐屙孱舂翦舂ㄤ彐躅痱轭舡轸犰殂屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê轸犰殂萤ㄩ翎扉悱屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄢ镬洵屐屙孱ê痱轭舡骢钽糸镱痱轭舡怙熹屐屙孱舂翦舂ㄤ彐躅痱轭舡怙熹屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê怙熹萤ㄢ镬洵屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄣ镤瀛屐屙孱ê痱轭舡骢钽糸镱痱轭舡泔溴屐屙孱舂翦舂ㄤ彐躅痱轭舡泔溴屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê泔溴萤ㄣ镤瀛屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄤ镢篝蜷铉镳糸镱屐屙孱ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛镳糸镱屐屙孱舂钺礤鲠祯濠ㄤ彐躅痱轭舡滹泱趄轭绛镳糸镱屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê镳糸镱侯犴忽犰蹂萤ㄤ镢篝蜷铉镳糸镱屐屙孱舡钺礤屐屙ㄤ镢篝蜷铉镳糸镱屐屙孱舡鲠祯屐屙┅ㄤ彐篝蝓泗ㄤ镢篝蜷铉镳糸镱蟓屐屙孱ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛镳糸镱蟓屐屙孱舂镳糸镱螬ㄤ彐躅痱轭舡滹泱趄轭绛镳糸镱蟓屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê镳糸镱窿ㄤ镢篝蜷铉镳糸镱蟓屐屙孱舡镳糸镱屐屙┅ㄤ彐篝蝓泗蝈姝屐屙孱ê痱轭舡骢钽糸镱痱轭舡蝈姝屐屙孱舂钺礤豉疱ㄤ彐躅痱轭舡蝈姝屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê蝈纩累ê豉疱俩萤蝈姝屐屙孱舡豉疱屐屙篝蜷铉躔汜箦蝈姝屐屙孱舡钺礤屐屙┅┅换蔑眄犷潴ㄤ彐篝蝓泗ㄣ镯磲钿屐屙孱ê痱轭舡骢钽糸镱痱轭舡泔眄犷洵屐屙孱舂钺礤镳糸镱狎珞ㄤ彐躅痱轭舡泔眄犷洵屐屙孱ㄣ镯磲钿篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê泔眄犷埝窿蔺笼)"
          (command-element-name command)
          (command-element-options command)
          (command-element-args command)))

;; Specific commands
(defun arg-command-p (command)
  "Checks whether `command` is an **arg** command

   TODO: Check that command options and arguments are valid."
  
  (and (command-element-p command)
       (equalp (command-element-name command) "arg")))

(defstruct (args-element
             (:print-function print-args-element))
  args)

(defun print-args-element (args stream depth)
  (format stream "(:args 窿ㄡ蜱蟓屐屙孱舡狎珞狎珞┅ㄤ彐篝蝓泗ㄡ蜱屐屙孱ê痱轭舡骢钽糸镱痱轭舡狎绛屐屙孱舂钺礤豉疱溴筱蜷痿轱瞟ㄤ彐躅痱轭舡狎绛屐屙孱ㄡ蜱篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê狎呼疱湖轰弩泸轲糸镱萤ㄡ蜱屐屙孱舡钺礤狎绌ㄡ蜱屐屙孱舡豉疱狎绌ㄡ蜱屐屙孱舡溴筱蜷痿轱狎绌┅ㄤ彐躅泔钽狒轭忮赭邋瞽翦翳轭珞戾è蝈篚祠铋飑祜镳骘翳轭轭翳轭珞滹ㄩ铒篝蜷铉翳轭绌瘐箬翳轭蝈篚祠换屐箦戾è沲蝌孱舡翳轭ㄦ轵篝蝈篚祠┅ㄩ篝蜷铉沲蝌孱舡翳轭绌痱镧痫蝈篚祠瘐箬ㄣ镱汜翦钺翦篝蜷铉沲蝌孱舡翳轭翳轭绌蝈篚祠┅换屐箦瘐箬翳轭蝈篚祠┅┅铗弼弪箦蝈篚祠┅ㄤ彐躅泔钽狒轭忮赭邋瞽翦舄翳轭珞ㄣ镱汜舡轭忮赭邋瞽翦ㄦ灬趑孱翳轭珞┅ㄤ彐躅铒蝽犰辁瀛磲螂躔翦翳轭珞脲趄轫扉篝＼五魈轭＼┅戾è翳轭珞ㄣ镱汜舡轭忮赭邋瞽翦舄翳轭珞┅ㄩㄡ钿ㄥ聃犰戾铉翳翳轭珞暴篝蜷铉ㄦ轵篝翳轭珞┅篝蜷铉趄轫趄轫ㄦ轵篝翳轭珞┅戾è忮珑轭轭ㄦ轵篝翳轭珞┅ㄥ钿轭ㄣ狎灬篝翳轭珞┅┅ㄡ痧孱扉篝矧ㄡ钿篝蜷铉忮珑轭轭绌篝蜷铉戾骠趄轫趄轫忮珑轭轭绌忮珑轭轭绌ㄢ豸灬篝ㄣ潋翳轭珞┅扉篝矧ㄡ钿篝蜷铉孱溟铉篝蜷铉蜷玷舡趄轫趄轫孱溟铉┅孱溟铉┅┅┅ㄤ彐蝓戾屣＼五魈轭濠ㄤ彐蝓戾屣āㄣ栳蜥泗弪汨狎徙翦颟┅ㄤ彐蝓戾忪犷＼ㄤ彐蝓戾翎＼葬猢ㄤ彐蝓戾箴徙轭í矧忪犷翎猢ê翦舂ㄤ彐蝓戾箴徙轭绐í矧屣忪犷翎猢ê翦舂ㄤ彐蝓戾戾趑弪ㄣ栳蜥泗弪蜥铉弩ǎ茚＼ǎ芰＼讴ǎ馨＼供┅ㄤ彐蝓戾黠蜾ㄡ钿ǐ铒矧忪犷翎屣屣姗┅é矧忪犷翎屣屣姗┅ê骢钽糸镱灬礅溽磲翥瑭翦ㄦ轵篝磲翥瑭┅┅ㄤ彐蝓戾黠蜾箦疳蜥麸矧忪犷翎屣屣＼＼＼＼＼＼＼＼＼＼┅ㄤ彐蝓戾黠蜾ㄡ钿ǐ铒黠蜾箦疳蜥麸颟é黠蜾箦疳蜥麸颟ê骢钽糸镱灬礅溽磲翥瑭翦ㄦ轵篝磲翥瑭┅┅ㄤ彐蝓戾扉篝屐屙孱ǐㄡ钿扉篝轸屙屣飑┅ê骢钽糸镱灬礅溽磲翥瑭磲脲扉篝屐屙孱洪翦眢磲疸狎＇骈蝮磲翥瑭┅┅ㄤ彐蝓戾扉篝轸屙ㄡ钿箴徙轭ā怙熹屐屙孱舂＼扉篝轸屙翦舂ê骢钽糸镱灬礅溽磲翥瑭磲脲扉篝轸屙屐屙孱呼屮铘磲翥瑭┅┅ㄤ彐蝓戾扉篝轸屙翦ㄡ钿ā矧ㄡ钿屣扉篝轸屙ㄡ钿屣屣飑┅屣飑磲螂躔翦舡扉铄扉篝轸屙翦舂ê骢钽糸镱铒蝽犰辁瀛磲螂躔翦舂ㄤ彐蝓戾泔溴屐屙孱ㄡ钿＼＼泔溴翦＼＼喋ê骢钽糸镱灬礅溽磲翥瑭磲脲泔溴屐屙孱呼屮铘磲翥瑭┅┅ㄤ彐蝓戾泔溴翦í铒ㄡ钿＼＼喋┅ê翦舂ㄤ彐蝓戾怙熹屐屙孱ㄡ钿＼＼怙熹屐屙孱舡翦＼＼ê骢钽糸镱灬礅溽磲翥瑭磲脲怙熹屐屙孱呼屮铘磲翥瑭┅┅ㄤ彐蝓戾怙熹屐屙孱舡翦í铒ㄡ钿＼＼┅ê翦舂ㄤ彐蝓戾轸犰殂屐屙孱ㄡ钿＼＼轸犰殂屐屙孱舡翦＼＼ê骢钽糸镱灬礅溽磲翥瑭磲脲轸犰殂屐屙孱呼屮铘磲翥瑭┅┅ㄤ彐蝓戾轸犰殂屐屙孱舡翦í铒ㄡ钿＼＼┅ê翦舂ㄤ彐蝓戾磲螂躔屐屙孱矧麸瓠磲螂躔屐屙孱篚猸磲螂躔屐屙孱舂ㄤ彐蝓戾篚猸磲螂躔屐屙孱矧泔溴屐屙孱怙熹屐屙孱轸犰殂屐屙孱蝈驽蝈钽泔眄犷洎ㄤ彐蝓戾麸瓠磲螂躔屐屙孱矧扉篝屐屙孱扉篝轸屙┅ㄤ彐蝓戾翦舡扉铄ǐㄡ钿箴徙轭黠蜾箴徙轭绌ê翦舂ㄤ彐蝓戾翦ǐㄡ钿箴徙轭绐黠蜾箴徙轭绐┅ê翦舂ㄤ彐蝓戾磲螂躔翦舡扉铄矧ㄡ钿箴徙轭磲螂躔屐屙孱箴徙轭篚猸磲螂躔翦舡扉铄ㄡ钿箴徙轭磲螂躔屐屙孱舂ㄡ钿箴徙轭黠蜾箴徙轭篚猸磲螂躔翦舡扉铄ㄡ钿箴徙轭黠蜾┅ê骢钽糸镱铒蝽犰辁瀛磲螂躔翦舂ㄤ彐蝓戾篚猸磲螂躔翦舡扉铄矧ㄡ钿箴徙轭篚猸磲螂躔屐屙孱箴徙轭篚猸磲螂躔翦舡扉铄ㄡ钿箴徙轭篚猸磲螂躔屐屙孱舂ㄡ钿箴徙轭黠蜾箴徙轭篚猸磲螂躔翦舡扉铄ㄡ钿箴徙轭黠蜾┅ㄤ彐蝓戾磲螂躔翦íㄡ钿磲螂躔翦舡扉铄矧屣屣姗┅ê骢钽糸镱铒蝽犰辁瀛磲螂躔翦舂换娘泱趄轭镳糸镱ㄤ彐蝓戾滹泱趄轭绛镳糸镱钺礤ǐ铒矧＼＼忪犷翎屣飑┅ê翦舂ㄤ彐蝓戾滹泱趄轭绛镳糸镱鲠祯ǐ铒矧＼＼忪犷翎屣飑┅ê翦舂ㄤ彐蝓戾滹泱趄轭绛镳糸镱矧ㄡ钿滹泱趄轭绛镳糸镱钺礤箴徙轭＼箴徙轭滹泱趄轭绛镳糸镱鲠祯濠滹泱趄轭绛镳糸镱钺礤ê骢钽糸镱灬礅溽磲翥瑭ㄩ扉篝磲翥瑭磲脲滹泱趄轭绛镳糸镱屐屙孱侯犴ㄦ轵篝磲翥瑭忽犰蹂铘磲翥瑭磲脲滹泱趄轭绛镳糸镱屐屙孱侯犴磲翥瑭┅┅ㄤ彐蝓戾滹泱趄轭绛镳糸镱ㄡ钿＼ǐㄡ钿箴徙轭滹泱趄轭绛镳糸镱箴徙轭＼哗┅ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛镳糸镱蟓屐屙孱猴痿轱铙磲疸狎＇箦泔钿铘磲翥瑭┅┅换义驽蝈钽弩ㄤ彐蝓戾躔汜箦汨狎徙翦铒矧忪犷翎屣ㄣ栳蜥泗弪蜥铉弩ǎ茚＼┅┅ㄤ彐蝓戾躔汜箦黠蜾ㄡ钿ǐ躔汜箦汨狎徙翦颟é矧翎忪犷屣屣姗┅ê骢钽糸镱灬礅溽磲翥瑭翦ㄦ轵篝磲翥瑭┅┅ㄤ彐蝓戾蝈驽蝈钽ㄡ钿＼蝈驽蝈钽瀛翦＼ㄡ钿箴徙轭＼箴徙轭蝈驽蝈钽瀛豉疱箴徙轭＼┅┅ê骢钽糸镱灬礅溽磲翥瑭ㄩ篝蜷铉磲翥瑭磲脲蝈姝屐屙孱侯犴磲翥瑭戾è钺礤箦泔钿磲翥瑭豉疱铘铘磲翥瑭┅磲脲蝈姝屐屙孱侯犴钺礤呼疱豉疱┅┅┅ㄤ彐蝓戾蝈驽蝈钽瀛翦ǐ铒＼喋ê翦舂ㄤ彐蝓戾蝈驽蝈钽瀛豉疱ǐ铒矧翎忪犷屣＼┅┅ê翦舂换蔑眄犷潴ㄤ彐蝓戾泔眄犷ㄡ钿矧＼＼扩泔眄犷洵钺礤泔眄犷洵镳糸镱螬泔眄犷洵狎珞┅ê骢钽糸镱灬礅溽磲翥瑭ㄤ弩趄蹉趱蜷铉忾钿脲黠蜾钺礤镳糸镱狎珞磲翥磲脲泔眄犷洵屐屙孱侯犴钺礤猴痿轱铙镳糸镱横蜱狎珞┅┅ㄤ彐蝓戾泔眄犷洵狎珞í泔眄犷洵狎绌ㄤ彐蝓戾泔眄犷洵狎ㄡ钿＼箴徙轭íㄡ钿ā＼矧磲螂躔屐屙孱泔眄犷洵黠蜾箴徙轭绌＼ê骢钽糸镱灬礅溽磲翥瑭铒蝽犰辁瀛磲螂躔翦翳轵磲翥瑭┅┅ㄤ彐蝓戾泔眄犷洵黠蜾ㄡ钿ǐ铒矧忪犷翎屣屣＼┅é矧忪犷翎屣屣＼┅ê骢钽糸镱灬礅溽磲翥瑭翦ㄦ轵篝磲翥瑭┅┅ㄤ彐蝓戾泔眄犷洵钺礤黠蜾ê翦舂ㄤ彐蝓戾泔眄犷洵镳糸镱ㄡ钿＼泔眄犷洵镳糸镱蟓扉篝＼荸ê骢钽糸镱灬礅溽磲翥瑭箦泔钿磲翥瑭┅ㄤ彐蝓戾泔眄犷洵镳糸镱蟓扉篝矧ㄡ钿箴徙轭泔眄犷洵镳糸镱箴徙轭＼泔眄犷洵镳糸镱蟓扉篝ㄡ钿箴徙轭泔眄犷洵镳糸镱箴徙轭绌ê骢钽糸镱灬礅溽磲翥瑭ㄣ镱箦泔钿磲翥瑭铘磲翥瑭┅┅ㄤ彐蝓戾泔眄犷洵镳糸镱ㄡ钿泔眄犷洵镳糸镱钺礤箴徙轭ㄡ钿矧＼＼憨箴徙轭泔眄犷洵镳糸镱鲠祯濠┅ê骢钽糸镱灬礅溽磲翥瑭ㄣ镱ㄦ轵篝磲翥瑭翳轵翳轵磲翥瑭┅┅ㄤ彐蝓戾泔眄犷洵镳糸镱钺礤ㄡ钿ǐ铒矧黠蜾箦疳蜥麸＼僵┅é矧黠蜾箦疳蜥麸＼僵┅ê骢钽糸镱灬礅溽磲翥瑭翦ㄦ轵篝磲翥瑭┅┅ㄤ彐蝓戾泔眄犷洵镳糸镱鲠祯ㄡ钿ǐ铒矧忪犷翎屣屣＼＼＼＼＼＼荸┅ê翦舂换硫珞ㄤ彐蝓戾狎珞屐屙孱矧泔眄犷洵狎珞屐屙孱翦趱犰狎珞屐屙孱舂ㄤ彐蝓戾翦趱犰狎珞屐屙孱ㄡ钿矧⒘蜱蠛⑿狎犴蠛箴徙轭绐ǐㄡ钿狎绛屐屙孱箴徙轭绐┅ê骢钽糸镱灬礅溽磲翥瑭磲脲狎珞屐屙孱横蜱磲疸狎＇骈蝮翳轵磲翥瑭┅┅ㄤ彐蝓戾泔眄犷洵狎珞屐屙孱ǐㄡ钿ㄡ蜱泔眄犷洵泔眄犷洎箴徙轭绐┅ê骢钽糸镱灬礅溽磲翥瑭ㄦ戾è磲脲狎ㄣ镯磲钿磲脲狎绛屐屙孱侯犴ㄣ镯磲钿屐屙孱舡钺礤泔眄犷洎呼疱ㄣ狎ㄦ轵篝ㄣ镯磲钿屐屙孱舡镳糸镱泔眄犷洎┅轰弩泸轲糸镱ㄦ轵篝ㄣ镯磲钿屐屙孱舡狎珞泔眄犷洎┅┅磲脲狎珞屐屙孱横蜱磲疸狎ㄡ戾犷潋獒恒镯痫箦＇磲脲狎＇骈蝮舂磲翥瑭┅┅ㄤ彐蝓戾狎绛屐屙孱ㄡ钿箴徙轭狎绛钺礤箴徙轭ㄡ钿狎绛豉疱箴徙轭绌⒑箴徙轭狎绛溴筱蜷痿轱瞟ê骢钽糸镱灬礅溽磲翥瑭磲脲狎绛屐屙孱侯犴翳轵磲翥瑭呼疱ㄦ轵篝铘磲翥瑭轰弩泸轲糸镱铘磲翥瑭┅┅ㄤ彐蝓戾狎绛钺礤黠蜾ㄤ彐蝓戾狎绛豉疱ㄡ钿＼狎绛豉疱钺礤＼┅ê骢钽糸镱灬礅溽磲翥瑭箦泔钿磲翥瑭┅ㄤ彐蝓戾狎绛豉疱钺礤黠蜾ㄤ彐蝓戾狎绛溴筱蜷痿轱矧ㄡ钿磲螂躔翦舡扉铄屣āㄡ钿箴徙轭矧狎绛屐屙孱蝈趱蝾蟓屐屙孱舂┅狎绛溴筱蜷痿轱瞟磲螂躔翦舡扉铄ê骢钽糸镱灬礅溽磲翥瑭铒蝽犰辁瀛磲螂躔翦磲翥瑭┅ㄤ彐篝蝓泗蝈趱蝾蟓屐屙孱ê痱轭舡骢钽糸镱痱轭舡蝈趱蝾蟓屐屙孱舂蝈趱蝾螬ㄤ彐躅痱轭舡蝈趱蝾蟓屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê蝈趱蝾萤蝈趱蝾蟓屐屙孱舡蝈趱蝾屐屙┅ㄤ彐躅蝈趱蝾蟓泔眄犷洵ㄣ镯磲钿ㄡ钿ㄣ镯磲钿屐屙孱舡泔眄犷洎ㄥ聃犰ㄣ镯磲钿屐屙孱舡钺礤泔眄犷洎Ⅱ弭躜铙┅ㄤ彐蝓戾蝈趱蝾蟓屐屙孱矧泔眄犷洵蝈趱蝾蟓屐屙孱翦趱犰蝈趱蝾蟓屐屙孱舂ㄤ彐蝓戾泔眄犷洵蝈趱蝾蟓屐屙孱蝈趱蝾蟓泔眄犷洵泔眄犷洎ê骢钽糸镱灬礅溽磲翥瑭磲脲蝈趱蝾蟓屐屙孱候弭躜铙ㄦ轵篝ㄣ镯磲钿屐屙孱舡狎珞磲翥瑭┅┅ㄤ彐蝓戾翦趱犰蝈趱蝾蟓屐屙孱ㄡ钿⒁弭躜铙孩箴徙轭蝈趱蝾溴筱蜷痿轱瞟ê骢钽糸镱灬礅溽磲翥瑭磲脲蝈趱蝾蟓屐屙孱候弭躜铙翳轵磲翥瑭┅┅ㄤ彐蝓戾蝈趱蝾溴筱蜷痿轱ㄡ钿磲螂躔翦舡扉铄ê骢钽糸镱灬礅溽磲翥瑭铒蝽犰辁瀛磲螂躔翦磲翥瑭┅ㄤ彐篝蝓泗ㄤ镢篝蜷铉礤翎溽翎ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛礤翎溽翎┅礤翎溽翎ㄤ彐躅痱轭舡滹泱趄轭绛礤翎溽翎礤翎溽翎篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê礤翎溽翎窿ㄤ镢篝蜷铉礤翎溽翎礤翎溽翎礤翎溽翎┅ㄤ彐蝓戾滹泱趄轭绛礤翎溽翎ǐㄡ钿箴徙轭矧滹泱趄轭绛鲥蝮轱滹泱趄轭绛麸滹滹泱趄轭绛箦滹泱趄轭绛溽翦滹泱趄轭绛狨翳矧滹泱趄轭绛翎珞矧屣屣姗┅ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛礤翎溽翎喉弭徜狒磲疸狎＇箦泔钿磲翥瑭┅┅换狨翳矧ㄤ彐篝蝓泗ㄤ镢篝蜷铉狨翳矧ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛狨翳矧┅狨翳矧ㄤ彐躅痱轭舡滹泱趄轭绛狨翳矧ㄡ豸栾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê狨翳矧萤ㄤ镢篝蜷铉狨翳矧狨翳矧狨翳矧┅ㄤ彐蝓戾滹泱趄轭绛狨翳矧ㄡ钿⒘豸栾蚝箴徙轭磲螂躔翦舡扉铄ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛狨翳矧横豸栾翳轵磲翥瑭┅┅换鲥蝮轱ㄤ彐篝蝓泗ㄤ镢篝蜷铉鲥蝮轱ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛鲥蝮轱瞟鲥蝮轱瞟ㄤ彐躅痱轭舡滹泱趄轭绛鲥蝮轱鲥蝮轱篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê鲥蝮轱萤ㄤ镢篝蜷铉鲥蝮轱瞽鲥蝮轱鲥蝮轱瞟┅ㄤ彐蝓戾滹泱趄轭绛鲥蝮轱ㄡ钿⒅弪箝镱孩箴徙轭磲螂躔翦舡扉铄ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛鲥蝮轱忽弪箝镱翳轵磲翥瑭┅┅换溽翦ㄤ彐篝蝓泗ㄤ镢篝蜷铉溽翦ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛溽翦┅溽翦ㄤ彐躅痱轭舡滹泱趄轭绛溽翦ㄤ狒篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê溽翦萤ㄤ镢篝蜷铉溽翦溽翦溽翦┅ㄤ彐蝓戾滹泱趄轭绛溽翦ㄡ钿⒛狒搴箴徙轭磲螂躔翦舡扉铄ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛溽翦轰狒翳轵磲翥瑭┅┅换韵南ㄤ彐篝蝓泗ㄤ镢篝蜷铉麸滹ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛麸滹┅麸滹ㄤ彐躅痱轭舡滹泱趄轭绛麸滹麸滹篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê韵南萤ㄤ镢篝蜷铉麸滹麸滹麸滹┅ㄤ彐蝓戾滹泱趄轭绛麸滹ㄡ钿⒃夏虾箴徙轭磲螂躔翦舡扉铄ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛麸滹呼镤翳轵磲翥瑭┅┅换渝ㄤ彐篝蝓泗ㄤ镢篝蜷铉箦ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛箦濠蝈驽蝈钽弩ㄤ彐躅痱轭舡滹泱趄轭绛箦箦篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴ê箦窿ㄤ镢篝蜷铉箦瀛蝈驽蝈钽弩箦濠┅ㄤ彐蝓戾滹泱趄轭绛箦ㄡ钿⒂邋孩箴徙轭滹泱趄轭绛箦瀛蝈驽蝈钽弩ê骢钽糸镱灬礅溽磲翥瑭磲脲滹泱趄轭绛箦候彐弪孱沐翳轵磲翥瑭┅┅ㄤ彐蝓戾滹泱趄轭绛箦瀛蝈驽蝈钽弩矧ㄡ钿箴徙轭蝈驽蝈钽箴徙轭＼箴徙轭滹泱趄轭绛箦瀛蝈驽蝈钽弩ㄡ钿箴徙轭蝈驽蝈钽濠ê骢钽糸镱灬礅溽磲翥瑭ㄣ镱箦泔钿磲翥瑭铘磲翥瑭┅┅换葬珞ㄤ彐篝蝓泗ㄤ镢篝蜷铉翎珞ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛翎珞┅翎珞ㄤ彐躅痱轭舡滹泱趄轭绛翎珞翎珞篝蝈犴溴麴瑭ㄦ矧磲篝蝈犴ê翎珞誉蕃)" (docstring-tags-tags tags)))

(defrule docstring-tags (and "Tags:" spacing tags-list)
  (:function (lambda (match)
	       (make-docstring-tags :tags
				    (third match)))))

(defrule tags-list (or (and spacing docstring-tag
			    spacing #\, spacing
			    tags-list)
		       (and spacing docstring-tag))
  (:function (lambda (match)
	       (cons (second match)
		     (nth 5 match)))))

(defrule docstring-tag word*)

;; Categories

;; Docstrings

;; Function docstring

(defstruct (function-docstring
             (:print-function print-function-docstring))
  options
  short-description
  args
  returns
  long-description
  metadata)

(defun print-function-docstring (docstring stream depth)
  (format stream "(:function-docstring")
  (when (function-docstring-options docstring)
    (format stream " :options A" (function-docstring-options docstring)))
  (when (function-docstring-short-description docstring)
    (format stream " :short-description S" (function-docstring-short-description docstring)))
  (when (function-docstring-args docstring)
    (format stream " :args A" (function-docstring-args docstring)))
  (when (function-docstring-returns docstring)
    (format stream " :returns A" (function-docstring-returns docstring)))
  (when (function-docstring-long-description docstring)
    (format stream " :long-description S" (function-docstring-long-description docstring)))
  (when (function-docstring-metadata docstring)
    (format stream " :metadata A" (function-docstring-metadata docstring)))
  (format stream ")"))

;; Class docstring

(defstruct (class-docstring
             (:print-function print-class-docstring))
  options
  description
  metadata)

(defun print-class-docstring (docstring stream depth)
  (format stream "(:class-docstring")
  (when (class-docstring-options docstring)
    (format stream " :options A" (class-docstring-options docstring)))
  (when (class-docstring-description docstring)
    (format stream " :description S" (class-docstring-description docstring)))
  (when (class-docstring-metadata docstring)
    (format stream " :metadata A" (class-docstring-metadata docstring)))
  (format stream ")"))

(defrule class-docstring
    (and (? docstring-options)
         (? (and spacing* docstring-long-description))
         (? (and spacing* docstring-metadata))
         spacing*)
  (:function (lambda (match)
               (destructuring-bind (options
                                    description
                                    metadata
                                    spacing) match
                 (make-class-docstring
                  :options options
                  :description (second description)
                  :metadata (second metadata))))))

;; Package docstring

(defstruct (package-docstring
             (:print-function print-package-docstring))
  options
  description
  metadata)

(defun print-package-docstring (docstring stream depth)
  (format stream "(:package-docstring")
  (when (package-docstring-options docstring)
    (format stream " :options A" (package-docstring-options docstring)))
  (when (package-docstring-description docstring)
    (format stream " :description S" (package-docstring-description docstring)))
  (when (package-docstring-metadata docstring)
    (format stream " :metadata A" (package-docstring-metadata docstring)))
  (format stream ")"))

(defrule package-docstring
    (and (? docstring-options)
         (? (and spacing* docstring-long-description))
         (? (and spacing* docstring-metadata))
         spacing*)
  (:function (lambda (match)
               (destructuring-bind (options
                                    description
                                    metadata
                                    spacing) match
                 (make-package-docstring
                  :options options
                  :description (second description)
                  :metadata (second metadata))))))


;; Docstring parsing

(defrule docstring-element (or args-element
                               returns-element
                               docstring-metadata))

(defrule function-docstring
    (and (? docstring-options) spacing* docstring-short-description
         (? (and spacing* args-element))
         (? (and spacing* returns-element))
         (? (and spacing* docstring-long-description))
         (? (and spacing* docstring-metadata))
         spacing*)
  (:function (lambda (match)
               (destructuring-bind (options sp1 short-description
                                            args
                                            returns
                                            long-description
                                            metadata
                                            spacing) match
                 (make-function-docstring
                  :options options
                  :short-description short-description
                  :args (second args)
                  :returns (second returns)
                  :long-description (second long-description)
                  :metadata (second metadata))))))

(defrule docstring-short-description (and (! (or (and spacing docstring-element)
                                                 (and eol eol)))
                                          markup-text-line
                                          (? (and eol
                                                  docstring-short-description)))
  (:function (lambda (match)
               (normalize-markup-text (list (second match)
                                            (third match))))))

(defrule docstring-long-description (and (! (and spacing docstring-element))
                                         markup-text-line
                                         (? (and spacing*
                                                 docstring-long-description)))

  (:function (lambda (match)
               (normalize-markup-text (list (second match)
                                            (third match))))))

;; Api

(defun parse-function-docstring (docstring)
  (parse 'function-docstring docstring))

(defun parse-class-docstring (docstring)
  (parse 'class-docstring docstring))

(defun parse-class-slot-docstring (docstring)
  (parse 'markup-text docstring))

(defun parse-package-docstring (docstring)
  (parse 'package-docstring docstring))
