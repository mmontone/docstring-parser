(in-package #:docstring-parser)

(defstruct (list-element
	     (:print-function print-list-element))
  items)

(defun print-list-element (elem stream depth)
  (format stream "窿扉篝屐屙孱舡轸屙屐屙┅ㄤ彐篝蝓泗扉篝轸屙屐屙孱ê痱轭舡骢钽糸镱痱轭舡扉篝轸屙屐屙孱舂翦舂ㄤ彐躅痱轭舡扉篝轸屙屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴立扉篝轸屙屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄩ翎扉悱屐屙孱ê痱轭舡骢钽糸镱痱轭舡轸犰殂屐屙孱舂翦舂ㄤ彐躅痱轭舡轸犰殂屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴怜ㄩ翎扉悱屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄢ镬洵屐屙孱ê痱轭舡骢钽糸镱痱轭舡怙熹屐屙孱舂翦舂ㄤ彐躅痱轭舡怙熹屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴联ㄢ镬洵屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄣ镤瀛屐屙孱ê痱轭舡骢钽糸镱痱轭舡泔溴屐屙孱舂翦舂ㄤ彐躅痱轭舡泔溴屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴⑧帼拎啖ㄣ镤瀛屐屙孱舡翦屐屙┅ㄤ彐篝蝓泗ㄤ镢篝蜷铉镳糸镱屐屙孱ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛镳糸镱屐屙孱舂钺礤鲠祯濠ㄤ彐躅痱轭舡滹泱趄轭绛镳糸镱屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴梁立ㄤ镢篝蜷铉镳糸镱屐屙孱舡钺礤屐屙ㄤ镢篝蜷铉镳糸镱屐屙孱舡鲠祯屐屙┅ㄤ彐篝蝓泗ㄤ镢篝蜷铉镳糸镱蟓屐屙孱ê痱轭舡骢钽糸镱痱轭舡滹泱趄轭绛镳糸镱蟓屐屙孱舂镳糸镱螬ㄤ彐躅痱轭舡滹泱趄轭绛镳糸镱蟓屐屙孱ㄥ戾篝蝈犴溴痿瑭ㄦ矧磲篝蝈犴〓窿藁" (docstring-options-element-options elem)))

(defstruct (ref-element
	     (:print-function print-ref-element))
  name
  type)

(defun print-ref-element (elem stream depth)
  (format stream "A" (string-upcase (ref-element-name elem)))
  (when (ref-element-type elem)
    (format stream " (A)" (ref-element-type elem))))

(defun concat-inbetween-text (things)
  (let ((result nil))
    (loop for thing in things
	 do
	 (if (not (stringp thing))
	     (push thing result)
	     ;; else
	     (let ((current-thing (first result)))
	       (if (stringp current-thing)
		   (progn
		     (pop result)
		     (push (concatenate 'string current-thing thing) result))
		   ;; else
		   (push thing result)))))
    (nreverse result)))

(defun concat-inbetween-text* (things)
  (concat-inbetween-text (flatten things)))

(defun normalize-markup-text (things)
  (let ((things (concat-inbetween-text* things)))
    (if (and (equalp (length things) 1)
	     (stringp (first things)))
	(first things)
	things)))


(defrule eol #\NewLine)

(defrule eof (! (characterp character)))

(defrule blank #\  )

(defrule tab #\Tab)

(defrule spacing (* (or blank tab eof))
  (:text t))

(defrule spacing* (* (or eol blank tab eof))
  (:text t))

(defrule letter
    (character-ranges (#\a #\z)
		      (#\A #\Z)
		      (#\0 #\9)))

(defrule word (and
	       (+ (not (or blank tab eol eof)))
	       (& (or blank tab eol eof)))
  (:text t))

(defrule list-element (and eol (+ (and list-item (? eol))))
  (:function (lambda (match)
	       (make-list-element :items (mapcar #'first (second match))))))

(defrule list-item (and spacing #\* list-item-text)
  (:function (lambda (match)
	       (make-list-item-element :text (third match)))))

(defrule list-item-text (and (! (or (and eol list-item)
				    (and eol eol)))
			     (? eol)
			     markup-text-line
			     (? list-item-text))
  (:function normalize-markup-text))

(defrule code-element (and #\` #\` code-text #\` #\`)
  (:function (lambda (match)
	       (make-code-element :text (nth 2 match)))))

(defrule code-text (* (not (and #\` #\`)))
  (:text t))

(defrule bold-element (and #\* #\* bold-element-text #\* #\*)
  (:function (lambda (match)
	       (make-bold-element :text (nth 2 match)))))

(defrule bold-element-text (* (not (and #\* #\*)))
  (:text t))

(defrule italic-element (and #\/ #\/ italic-element-text #\/ #\/)
  (:function (lambda (match)
	       (make-italic-element :text (nth 2 match)))))

(defrule italic-element-text (* (not (and #\/ #\/)))
  (:text t))

(defrule markup-element (or list-element
			    code-element
			    bold-element
			    italic-element))

(defrule text-line (+ (and spacing word spacing))
  (:text t))

(defrule text (+ (and spacing* word spacing*))
  (:text t))

(defrule markup-text-line (or (and spacing markup-element spacing markup-text-line)
			      (and spacing word  spacing markup-text-line)
			      (and spacing markup-element)
			      (and spacing word))
  (:function normalize-markup-text))

(defrule markup-text
    (or (and spacing* markup-element spacing* markup-text-line)
	(and spacing* word  spacing* markup-text-line)
	(and spacing* markup-element)
	(and spacing* word))
  (:function normalize-markup-text))

(defrule docstring-option-name (+ (not (or #\: #\; blank tab eol)))
  (:text t))

(defrule docstring-option-value (+ (not (or #\: #\; blank tab eol)))
  (:text t))

(defrule docstring-option (or (and docstring-option-name spacing #\: spacing docstring-option-value)
			      docstring-option-name)

  (:function (lambda (match)
	       (if (listp match)
		   (make-docstring-option-element :name (first match)
						  :value (nth 4 match))
		   (make-docstring-option-element :name match)))))

(defrule docstring-options (and #\! (+ (and spacing docstring-option spacing #\;)))
  (:function (lambda (match)
	       (make-docstring-options-element :options
					       (mapcar #'second (nth 1 match))))))

(defrule upcase-character (not (or blank tab eol
				   (character-ranges (#\a #\z)))))

(defrule upcase-word (and (+ upcase-character) (& (or tab blank eol eof)))
  (:text t))

(defrule reference (or upcase-word
		       (and #\` reference-text #\`
			    (? (and
				spacing #\( spacing
				reference-type
				spacing #\)))))
  (:function
   (lambda (match)
     (if (stringp match)
	 (make-ref-element :name match)
	 (let ((name (second match))
	       (type (nth 3 (nth 3 match))))
	   (make-ref-element :name name
			     :type type))))))
  
(defrule reference-text (+ (not #\`))
  (:text t))

(defrule reference-type (+ (not (or tab blank eol #\))))
  (:text t))
